#import "net.kai"
#import "mem.kai"
#import "curses.kai"

ComponentColor :: enum(u16) {
    Default :: 1
    Get
    Tabbar
    Tab
    LineNum
}

Color :: enum {
    Black    :: 0
    Red      :: 1
    Green    :: 2
    Yellow   :: 3
    Blue     :: 4
    Magenta  :: 5
    Cyan     :: 6
    White    :: 7
    DarkGrey :: 8
    Reverse  :: 255
}

Mode :: enum {
    Normal
    UrlInput
    Search
    Collections
    Endpoints
}

TabbarWidth  :: 0
TabbarHeight :: 1

HeaderWidth  :: 0
HeaderHeight :: 3
HeaderOffset :: TabbarHeight

MethodWidth  :: 5
MethodHeight :: 1

URL :: "https://jsonplaceholder.typicode.com/users"

EnableComponentColor :: fn(win: rawptr, color: ComponentColor) -> void {
    using curses
    colorPair := COLOR_PAIR(color)
    wattron(win, colorPair)
}

DisableComponentColor :: fn(win: rawptr, color: ComponentColor) -> void {
    using curses
    colorPair := COLOR_PAIR(color)
    wattroff(win, colorPair)
}

FillLine :: fn(win: rawptr, color: ComponentColor, width: i32) -> void {
    EnableComponentColor(win, color)
    for i := 0; i < width; i += 1 {
        curses.mvwprintw(win, 0, i, " ".raw)
    }
    DisableComponentColor(win, color)
}

FillLineOffset :: fn(win: rawptr, color: ComponentColor, width, offset: i32) -> void {
    EnableComponentColor(win, color)
    for i := 0; i < width; i += 1 {
        curses.mvwprintw(win, 0, i+offset, " ".raw)
    }
    DisableComponentColor(win, color)
}

Header :: struct {
    container,
    methodType,
    url: rawptr
}

SetupHeader :: fn(maxWidth: i32) -> Header {
    header: Header
    using curses
    return header
}

RefreshHeader :: fn(header: Header) -> void {
    curses.touchwin(header.container)
    curses.wrefresh(header.methodType)
    curses.wrefresh(header.container)
}

main :: fn() -> i32 {
    hnd := net.InitCurl()
    if !hnd {
        return 1
    }

    buffSize :: 1024 * 1024 * 4

    data: []u8
    mem.ArrayInit(mem.DefaultAllocatorInit(), &data, buffSize)

    using curses

    initscr()
    noecho()
    cbreak()

    hasComponentColor := has_colors() != FALSE
    if !hasComponentColor {
        return 1
    }

    use_default_colors()
    start_color()
    init_pair(ComponentColor.Get,     Color.White, Color.Green)
    init_pair(ComponentColor.Tabbar,  Color.White, Color.DarkGrey)
    init_pair(ComponentColor.Tab,     Color.Black, Color.Cyan)
    init_pair(ComponentColor.LineNum, Color.DarkGrey, -1)

    h, w := getmaxyx(stdscr)

    win := newwin(0, 0, 0, 0)
    keypad(win, TRUE)

    tabbar := subwin(win, TabbarHeight, TabbarWidth, 0, 0)
    header := subwin(win, HeaderHeight, HeaderWidth, HeaderOffset, 0)
    sub := derwin(header, 1, 9, 1, 1)
    url := derwin(header, 1, w-10, 1, 10)
    
    toggle := subwin(win, 1, 0, HeaderOffset+HeaderHeight, 0)
    content := derwin(win, h-6, 0, 5, 3)

    scrollok(content, TRUE)
    
    box(header, 0, 0)

    FillLine(tabbar, ComponentColor.Tabbar, w)
    FillLine(toggle, ComponentColor.Tabbar, w/2)
    // NOTE: the plus one is to properly size on odd screen widths
    FillLineOffset(toggle, ComponentColor.Tab, w/2+1, w/2)

    EnableComponentColor(tabbar, ComponentColor.Tab)
    mvwprintw(tabbar, 0, 0, " An example request + ".raw)
    DisableComponentColor(tabbar, ComponentColor.Tab)

    EnableComponentColor(sub, ComponentColor.Get)
    mvwprintw(sub, 0, 0, "   GET   ".raw)
    DisableComponentColor(sub, ComponentColor.Get)

    wprintw(url, " %s".raw, URL.raw)

    EnableComponentColor(toggle, ComponentColor.Tabbar)
    mvwprintw(toggle, 0, 1, "Request".raw)
    DisableComponentColor(toggle, ComponentColor.Tab)

    EnableComponentColor(toggle, ComponentColor.Tab)
    mvwprintw(toggle, 0, (w/2)+1, "Response".raw)
    DisableComponentColor(toggle, ComponentColor.Tab)

    EnableComponentColor(win, ComponentColor.LineNum)
    for i := 0; i < (h-6); i += 1 {
        mvwprintw(win, i+5, 0, "%*d".raw, 2, i+1)
    }
    DisableComponentColor(win, ComponentColor.LineNum)

    curs_set(FALSE)

    running := true
    for running {
        ch := wgetch(win)
        switch ch {
        case "q":
            running = false
        case KEY_ENTER:
            data.len = 0
            net.MakeRequest(hnd, net.Method.Get, URL, &data)
            code, status, time := net.Send(hnd)
            if net.OK(code) {
                curs_set(TRUE)
                wmove(win, 5, 3)
                wprintw(content, "%.*s\n".raw, data.len, data.raw)
            } else {
                wprintw(content, "%s\n".raw, net.ErrorToString(code).raw)
            }

            EnableComponentColor(toggle, ComponentColor.Tab)
            mvwprintw(toggle, 0, (w/2)+10, "(json, %dms, status: %d)   ".raw, cast(i32)(time*1000), status)
            DisableComponentColor(toggle, ComponentColor.Tab)

            touchwin(win)
            wrefresh(content)
            wrefresh(toggle)
        case KEY_UP:
            wscrl(content, -1) 
            touchwin(win)
            wrefresh(content)
        case KEY_DOWN:
            wscrl(content, 1)
            touchwin(win)
            wrefresh(content)
        case "e":
            wprintw(content, "Test\n".raw)
            wrefresh(content)
        case "f":
            ok := mem.WriteFile(".getboirc", "Hello, world!\n")
            if !ok {
                wprintw(content, "Oops!\n".raw)
                wrefresh(content)
            } else {
                wprintw(content, "Done! %d\n".raw, data.len)
                wrefresh(content)
            }
        }
    }
    
    endwin()
    return 0
}


